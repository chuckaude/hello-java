// Example Jenkinsfile with SIG Security Scan that implements:
// - Black Duck INTELLIGENT scan on pushes and RAPID scan on PRs to "important" branches
// - Coverity on Polaris FULL scan on pushes and PRs to "important" branches
pipeline {
    agent { label 'linux64' }
    environment {
        // production branches on which we want security reports
        PRODUCTION = "${env.BRANCH_NAME ==~ /^(stage|release)$/ ? 'true' : 'false'}"
        // full scan on pushes to important branches
        FULLSCAN = "${env.BRANCH_NAME ==~ /^(main|master|develop|stage|release)$/ ? 'true' : 'false'}"
        // PR scan on pulll requests to important branches
        PRSCAN = "${env.CHANGE_TARGET ==~ /^(main|master|develop|stage|release)$/ ? 'true' : 'false'}"
        // extract REPO_NAME from GIT_URL
        REPO_NAME = "${env.GIT_URL.tokenize('/.')[-2]}"
        // Bridge CLI download URL
        BRIDGECLI_LINUX64 = 'https://repo.blackduck.com/artifactory/bds-integrations-release/com/blackduck/integration/bridge/binaries/bridge-cli-bundle/latest/bridge-cli-bundle-linux64.zip'
    }
    tools {
        maven 'maven-3'
        jdk 'openjdk-21'
    }
    stages {
        stage('Build') {
            steps {
                sh 'mvn -B compile'
            }
        }
        stage('Test') {
            steps {
                sh 'mvn -B test'
            }
        }
        stage('Scan') {
            parallel {
                stage('Black Duck Full Scan') {
                    when { environment name: 'FULLSCAN', value: 'true' }
                    environment {
                        DETECT_PROJECT_NAME = "$REPO_NAME"
                        DETECT_PROJECT_VERSION_NAME = "$BRANCH_NAME"
                        DETECT_CODE_LOCATION_NAME = "$REPO_NAME-$BRANCH_NAME"
                        DETECT_RISK_REPORT_PDF = "${env.PRODUCTION == 'true' ? 'true' : 'false'}"
                    }
                    steps {
                        withCredentials([string(credentialsId: 'sca.field-test.blackduck.com', variable: 'BRIDGE_BLACKDUCK_TOKEN')]) {
                            script {
                                status = sh returnStatus: true, script: """
                                    curl -fLsS -o bridge.zip $BRIDGECLI_LINUX64 && unzip -qo -d $WORKSPACE_TMP bridge.zip && rm -f bridge.zip
                                    $WORKSPACE_TMP/bridge-cli-bundle-linux64/bridge-cli --stage blackducksca \
                                        blackduck.url=$BLACKDUCK_URL \
                                        blackducksca.scan.failure.severities='BLOCKER' \
                                        blackducksca.scan.full='true'
                                """
                                if (status == 8) { unstable 'policy violation' }
                                else if (status != 0) { error 'scan failure' }
                            }
                        }
                    }
                }
                stage('Black Duck PR Scan') {
                    // Bridge CLI PR comments currently not supported from Jenkins - see INTEGRATE-23
                    when { environment name: 'PRSCAN', value: 'true' }
                    environment {
                        DETECT_PROJECT_NAME = "$REPO_NAME"
                        DETECT_PROJECT_VERSION_NAME = "$CHANGE_TARGET"
                        DETECT_CODE_LOCATION_NAME = "$REPO_NAME-$CHANGE_TARGET"
                        BRIDGE_ENVIRONMENT_SCAN_PULL = 'true'
                    }
                    steps {
                        withCredentials([string(credentialsId: 'sca.field-test.blackduck.com', variable: 'BRIDGE_BLACKDUCK_TOKEN'),
                                         string(credentialsId: 'github-pat', variable: 'GITHUB_TOKEN')]) {
                            script {
                                status = sh returnStatus: true, script: """
                                    curl -fLsS -o bridge.zip $BRIDGECLI_LINUX64 && unzip -qo -d $WORKSPACE_TMP bridge.zip && rm -f bridge.zip
                                    $WORKSPACE_TMP/bridge-cli-bundle-linux64/bridge-cli --stage blackducksca \
                                        blackduck.url=$BLACKDUCK_URL \
                                        blackducksca.scan.full='false' \
                                        blackducksca.automation.prcomment='true' \
                                        github.repository.branch.name=$BRANCH_NAME \
                                        github.repository.name=$REPO_NAME \
                                        github.repository.owner.name=$CHANGE_AUTHOR \
                                        github.repository.pull.number=$CHANGE_ID \
                                        github.user.token=$GITHUB_TOKEN
                                """
                                if (status == 8) { unstable 'policy violation' }
                                else if (status != 0) { error 'scan failure' }
                            }
                        }
                    }
                }
                stage('Coverity on Polaris Full Scan') {
                    when { environment name: 'FULLSCAN', value: 'true' }
                    steps {
                        withCredentials([string(credentialsId: 'sipse.cop.blackduck.com', variable: 'POLARIS_ACCESS_TOKEN')]) {
                            script {
                                status = sh returnStatus: true, script: """
                                    curl -fLOsS $POLARIS_SERVER_URL/api/tools/polaris_cli-linux64.zip
                                    unzip -qo -d $WORKSPACE_TMP -jo polaris_cli-linux64.zip && rm -f polaris_cli-linux64.zip
                                    $WORKSPACE_TMP/polaris --co project.name=$REPO_NAME analyze -w
                                    # simple quality gate for critical and high impact issues; more advanced filtering requires an API script
                                    if [ \$(cat .blackduck/polaris/cli-scan.json | jq '[.issueSummary.issuesBySeverity|.critical,.high]|add') -ne 0 ]; then exit 8; fi
                                """
                                if (status == 8) { unstable 'policy violation' }
                                else if (status != 0) { error 'scan failure' }
                            }
                        }
                    }
                }
                stage('Coverity on Polaris PR Scan') {
                    when { environment name: 'PRSCAN', value: 'true' }
                    steps {
                        withCredentials([string(credentialsId: 'sipse.cop.blackduck.com', variable: 'POLARIS_ACCESS_TOKEN')]) {
                            script {
                                status = sh returnStatus: true, script: """
                                    curl -fLOsS $POLARIS_SERVER_URL/api/tools/polaris_cli-linux64.zip
                                    unzip -qo -d $WORKSPACE_TMP -jo polaris_cli-linux64.zip && rm -f polaris_cli-linux64.zip
                                    $WORKSPACE_TMP/polaris --co project.name=$REPO_NAME analyze -w
                                    # query for new issues; will always be non-zero for PRs; replace with API script to compare with CHANGE_TARGET
                                    if [ \$(cat .blackduck/polaris/cli-scan.json | jq '.issueSummary.newIssues') -ne 0 ]; then exit 8; fi
                                """
                                if (status == 8) { unstable 'policy violation' }
                                else if (status != 0) { error 'scan failure' }
                            }
                        }
                    }
                }
            }
        }
        stage('Deploy') {
            when { environment name: 'PRODUCTION', value: 'true' }
            steps {
                sh 'mvn -B -DskipTests install'
            }
        }
    }
    post {
        always {
            archiveArtifacts allowEmptyArchive: true, artifacts: '.blackduck/polaris/configuration/resolved-polaris.yml, .blackduck/polaris/data/coverity/*/idir/build-log.txt, *_BlackDuck_RiskReport.pdf'
            //zip archive: true, dir: '.bridge', zipFile: 'bridge-logs.zip'
            cleanWs()
        }
    }
}
