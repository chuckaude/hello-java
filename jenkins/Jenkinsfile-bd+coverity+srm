// example Jenkinsfile for standalone Black Duck, Coverity and SRM (Code DX) using Jenkins Plugins
// - Black Duck and Coverity full scans on push to specified branches with import into SRM
// - Black Duck Rapid and Coverity Comparison scans on pull requests with PR comments enabled
// https://plugins.jenkins.io/blackduck-security-scan/
// https://plugins.jenkins.io/codedx
pipeline {
    agent { label 'linux64' }
    environment {
        REPO_NAME = "${env.GIT_URL.tokenize('/.')[-2]}"
        FULLSCAN = "${env.BRANCH_NAME ==~ /^(main|master|develop|stage|release)$/ ? 'true' : 'false'}"
        PRSCAN = "${env.CHANGE_TARGET ==~ /^(main|master|develop|stage|release)$/ ? 'true' : 'false'}"
        GITHUB_TOKEN = credentials('github-pat')
        DETECT_PROJECT_NAME = "${env.REPO_NAME}"
    }
    tools {
        maven 'maven-3'
        jdk 'openjdk-21'
    }
    stages {
        stage('Build') {
            steps {
                sh 'mvn -B compile'
            }
        }
        stage('Test') {
            steps {
                sh 'mvn -B test'
            }
        }
        stage('Coverity') {
            when {
                anyOf {
                    environment name: 'FULLSCAN', value: 'true'
                    environment name: 'PRSCAN', value: 'true'
                }
            }
            steps {
                security_scan product: 'coverity',
                    coverity_url: 'https://coverity.field-test.blackduck.com',
                    coverity_project_name: "$REPO_NAME",
                    coverity_stream_name: "$REPO_NAME-$BRANCH_NAME",
                    coverity_args: "-o commit.connect.description=$BUILD_TAG",
                    coverity_policy_view: 'Outstanding Issues',
                    coverity_prComment_enabled: true,
                    coverity_local: true,
                    coverity_install_directory: "$COVERITY_TOOL_HOME",
                    github_token: "$GITHUB_TOKEN",
                    include_diagnostics: false,
                    mark_build_status: 'UNSTABLE'
            }
        }
        stage('Black Duck') {
            when {
                anyOf {
                    environment name: 'FULLSCAN', value: 'true'
                    environment name: 'PRSCAN', value: 'true'
                }
            }
            steps {
                security_scan product: 'blackducksca',
                    blackducksca_scan_failure_severities: 'BLOCKER',
                    blackducksca_prComment_enabled: true,
                    blackducksca_reports_sarif_create: true,
                    github_token: "$GITHUB_TOKEN",
                    include_diagnostics: false,
                    mark_build_status: 'UNSTABLE'
            }
        }
        stage('SRM') {
            when { environment name: 'FULLSCAN', value: 'true' }
            steps {
                step([
                    $class: 'CodeDxPublisher',
                    url: "$SRM_URL",
                    selfSignedCertificateFingerprint: '',
                    keyCredentialId: 'srm.field-test.blackduck.com',
                    project: [$class: 'NamedProject', projectName: "$REPO_NAME", autoCreate: true],
                    baseBranchName: 'main',
                    targetBranchName: "$BRANCH_NAME",
                    sourceAndBinaryFiles: '**',
                    analysisName: "Build [#$BUILD_TAG]($BUILD_URL)",
                    analysisResultConfiguration: [
                        policyBreakBuildBehavior: 'MarkUnstable',
                        failureSeverity: 'High',
                        failureOnlyNew: true,
                        unstableSeverity: 'High',
                        unstableOnlyNew: false,
                        numBuildsInGraph: 0
                    ]
                ])
            }
        }
        stage('Deploy') {
            steps {
                sh 'mvn -B install'
            }
        }
    }
    post {
        always {
            cleanWs()
        }
    }
}
